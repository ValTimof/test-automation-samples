plugins {
	id 'java'
	id 'jacoco'
	id "io.freefair.lombok" version "5.1.0"
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

ext {
	set 'junit-jupiter.version', '5.6.2'
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'jacoco'
	apply plugin: 'io.freefair.lombok'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.github.timofeevvr'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = JavaVersion.VERSION_14

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-starter:2.3.1.RELEASE") {
				bomProperties([
						'junit-jupiter.version': '5.6.2'
				])
			}
			mavenBom 'software.amazon.awssdk:bom:2.13.42'
		}
	}

	configurations.all {
		exclude module: 'spring-boot-starter-logging'
		exclude group: 'org.junit.vintage'
	}

	test {
		useJUnitPlatform()

		testLogging {
			exceptionFormat "full"
			events "started", "passed", "skipped", "failed", "standardOut", "standardError"
		}
	}

	jacoco {
		toolVersion '0.8.5'
	}

	jacocoTestReport {
		reports {
			xml.enabled true
			html.enabled true
		}
	}

	test.finalizedBy jacocoTestReport

	generateLombokConfig.enabled false
}